plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
    id 'io.freefair.lombok' version '6.6.1'
}

group 'ch.adesso.demo'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def allureVersion = "2.20.1",
    selenideVersion = "6.10.1",
    junitVersion = "5.9.1",
    slf4jVersion = "2.0.4",
    assertjCoreVersion = "3.23.1",
    ownerVersion = "1.0.12",
    aspectjweaverVersion = "1.9.9.1",
    restAssured = "5.3.0",
    jsonPath = "5.3.0",
    jsonSchemaValidator = "5.3.0",
    jacksonAnnotations = "2.14.1",
    jacksonDatatypeJoda = "2.14.1",
    lombokVersion = "1.18.24"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {

    testImplementation(
            "io.rest-assured:rest-assured:$restAssured",
            "io.rest-assured:json-path:$jsonPath",
            "io.rest-assured:json-schema-validator:$jsonSchemaValidator",
            "com.fasterxml.jackson.core:jackson-annotations:$jacksonAnnotations",
            "com.fasterxml.jackson.datatype:jackson-datatype-joda:$jacksonDatatypeJoda",
            "org.aspectj:aspectjweaver:$aspectjweaverVersion",
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "org.assertj:assertj-core:$assertjCoreVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "com.codeborne:pdf-test:1.8.0",
            "org.hamcrest:hamcrest-all:1.3",
            "org.projectlombok:lombok:$lombokVersion"
    )
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}